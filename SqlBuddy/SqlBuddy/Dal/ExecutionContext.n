using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Data;
using System.Data.SqlClient;
using System.Collections.Generic;
using System.Linq;

namespace SqlBuddy.Dal
{
    /// <summary>
    /// Description of ExecutionContext.
    /// </summary>
    public class ExecutionContext
        : IExecutionContext
    {
        private _connection : SqlConnection;
        private _transaction : SqlTransaction;
        private mutable _isConnectionClosed : bool;
        private _timeout : int;
        
        public this(connectionString : string, timeout : TimeSpan)
        {
            _connection = SqlConnection(connectionString);
            _transaction = null;
            _isConnectionClosed = true;
            _timeout = timeout.TotalSeconds :> int;
        }
        
        public Open() : void
        {
            _connection.Open();
            _isConnectionClosed = false;
        }
        
        public Close() : void
        {
            _connection.Close();
            _isConnectionClosed = true;
        }
        
        public ExecuteNonQuery(command : SqlCommand) : int
        {
            Prepare(command);

            command.ExecuteNonQuery();
        }
        
        public ExecuteReader(command : SqlCommand) : IDataReader
        {
            Prepare(command);

            command.ExecuteReader();
        }
        
        private Prepare(command : SqlCommand) : void
        {
            when (command == null) throw ArgumentNullException("command");

            OpenConnectionIfClosed();
            PrepareCommand(command);
        }
        
        private PrepareCommand(command : IDbCommand) : void
        {
            command.Connection = _connection;
            command.Transaction = _transaction;
            command.CommandTimeout = _timeout;
        }

        private OpenConnectionIfClosed() : void
        {
            when (_connection.State != ConnectionState.Open)
                Open();
        }
    }
}
