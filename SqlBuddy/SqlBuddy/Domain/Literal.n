using System;
using SCG = System.Collections.Generic;

using Nemerle.Assertions;
using Nemerle.Utility;
using Nemerle.Peg;

namespace SqlBuddy.Domain
{
    [Record]
    public variant Literal 
        : Located
    {
        | Null
        | Max
        | Output
        | Boolean { val : bool; }
        | Char    { val : char; }
        | String  { val         : string;
                    is_verbatim : bool; }
        | Real    { mantissa  : string }
        | Integer { val             : ulong;
                    is_hexadecimal  : bool; }
        | NegativeInteger { val             : long;
                    is_hexadecimal  : bool; }
        | ErrorOverflow { value : string; }
        | ErrorUnicode {  is_char : bool;
                          locations : list[Location]; }
        | ErrorChar

        public override ToString() : string
        {
            match (this)
            {
                | ErrorOverflow(x)              => $"Error:$x"
                | ErrorUnicode                  => "Error: Unrecognized unicode escape sequence"
                | ErrorChar                     => "Error: Too many characters in literal"
                | Null                          => "null"
                | Max                           => "max"
                | Output                        => "output"
                | Boolean(val)                  => if (val) "true" else "false"
                | Char(val)                     => $"'$val'"
                | String(val, _is_verbatim)     => $<#"$val"#>
                | Real(mantissa)                => $"$mantissa"
                | Integer(val, _is_hexadecimal) => $"$val"
                | NegativeInteger(val, _is_hexadecimal) => $"$val"
            }
        }
    }
}
